#!/usr/bin/env raku
use v6.d;

use WWW::Ollama::Client;
use JSON::Fast;

my %*SUB-MAIN-OPTS = :named-anywhere;

#| Ollama client invocation.
sub MAIN(
        *@words,
        Str :$path = 'completion',              #= Path, one of 'completion', 'chat', 'embedding', 'model-info', 'list-models', or 'list-running-models'.
        Str :m(:$model) is copy = 'gemma3:1b',  #= Model to use.
        Str :f(:$format) is copy = 'Whatever',  #= Format of the result; one of "json", "hash", "values", or "Whatever".
         ) {

    if $model.lc ∈ <whatever auto automatic> { $model = 'gemma3:1b' }
    if $format.lc ∈ <v value auto whatever> { $format = 'values' }

    my $ollama = WWW::Ollama::Client.new;

    my $ans;
    given $path.lc {
        when $_ ∈ <list-models models> {
            $ans = |$ollama.list-models;
        }
        when $_ ∈ <model-info> {
            $ans = $ollama.model-info(:$model);
        }
        when $_ ∈ <completion generation> {
            my %body = :$model, prompt => @words.join(' '), :!stream;
            $ans = $ollama.completion(%body);
        }
        when $_ ∈ <embed embedding embeddings> {
            my %body = :$model, input => @words;
            $ans = $ollama.embedding(%body);
        }
        when $_ ∈ <chat chat-completion> {
            # TODO Check for valid JSON message input and/or JSON file.
            my %body = :$model,
                       messages => [{role => "user", content => @words.join(' ')},];
            $ans = $ollama.chat(%body);
        }
        default {
            die 'Do not know how to process the value of the --path argument.'
        }
    }

    say do given $format.lc {
        when $_ ∈ <hash raku> { $ans.raku }
        when $_ eq 'values' {
            $path ~~ /embed/ ?? $ans<embeddings> !! $ans<content>
        }
        when $_ eq 'json' { to-json($ans, :pretty) }
        default { $ans }
    }
}
